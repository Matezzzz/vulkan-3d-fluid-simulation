#version 450

layout(local_size_x = 10, local_size_y = 10, local_size_z = 1) in;

const int detailed_resolution = 4;


layout(set = 0, binding = 0, r32ui)uniform restrict readonly uimage3D cell_types;
layout(set = 0, binding = 1, r32f) uniform restrict readonly  image3D densities_src;
layout(set = 0, binding = 2, r32f) uniform restrict writeonly image3D densities_dst;


const float diffuse_a = 0.1;


const int CELL_SOLID = 3;


uint cellAt(ivec3 i){
    return imageLoad(cell_types, i).x;
}
bool isSolid(uint a){
    return (a == CELL_SOLID);
}


float getDensity(ivec3 pos){
    return imageLoad(densities_src, pos).x;
}

void main(){
    ivec3 i = ivec3(gl_GlobalInvocationID.xyz);

    if (!isSolid(cellAt(i / detailed_resolution))){
        float d = ( 1.0 - 6 * diffuse_a) * getDensity(i) + diffuse_a *
        (getDensity(i + ivec3(1, 0, 0)) + getDensity(i - ivec3(1, 0, 0)) + 
         getDensity(i + ivec3(0, 1, 0)) + getDensity(i - ivec3(0, 1, 0)) + 
         getDensity(i + ivec3(0, 0, 1)) + getDensity(i - ivec3(0, 0, 1)));
    
        imageStore(densities_dst, i, vec4(d, 0.0, 0.0, 0.0));
    }
}